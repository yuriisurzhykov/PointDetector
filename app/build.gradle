plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "dagger.hilt.android.plugin"
    id "kotlin-kapt"
    id "kotlinx-serialization"
    id "com.google.gms.google-services"
    id 'com.google.firebase.appdistribution'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.yuriisurzhykov.pointdetector"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "GOOGLE_MAPS_API_KEY", ""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }

    }
    lintOptions {
        abortOnError false
    }
    signingConfigs {
        release {
            storeFile = file("${System.getenv("KEYSTORE_FILE")}")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }

        localRelease {
            def propertiesFile = new Properties()
            try {
                file("signing.properties").withInputStream { stream ->
                    propertiesFile.load(stream)
                }
                storeFile = file(propertiesFile.getProperty("KEYSTORE_FILE"))
                storePassword = propertiesFile.getProperty("STORE_PASSWORD").toString()
                keyAlias = propertiesFile.getProperty("KEY_ALIAS").toString()
                keyPassword = propertiesFile.getProperty("KEY_PASSWORD").toString()
            } catch(Exception e) {
                println("Exception: ${e}")
            }
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding = true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".dev"
            buildConfigField "String", "GOOGLE_MAPS_API_KEY", getDebugGoogleMapsApiKey()
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            buildConfigField "String", "GOOGLE_MAPS_API_KEY", getReleaseGoogleMapsApiKey()
            signingConfig signingConfigs.release
        }

        localRelease {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            buildConfigField "String", "GOOGLE_MAPS_API_KEY", getReleaseGoogleMapsApiKey()
            signingConfig signingConfigs.localRelease
            matchingFallbacks = ["release"]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {

    implementation project(":uiComponents")

    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "com.google.android.material:material:1.6.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.recyclerview:recyclerview-selection:1.1.0"
    implementation "androidx.databinding:databinding-runtime:7.3.1"
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.4"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.0"

    def room_version = "2.4.3"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    def hilt_version = "2.42"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    implementation "com.google.android.gms:play-services-location:21.0.1"

    implementation "androidx.activity:activity-ktx:1.6.1"
    implementation "androidx.fragment:fragment-ktx:1.5.5"

    implementation "com.google.maps.android:android-maps-utils:2.4.0"
    implementation "com.google.maps.android:maps-utils-ktx:3.4.0"
    implementation "com.google.maps.android:maps-ktx:3.2.1"

    implementation "com.google.android.gms:play-services-places:17.0.0"
    implementation "com.google.android.libraries.places:places:2.7.0"

    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.2"

    implementation platform('com.google.firebase:firebase-bom:30.3.2')
    implementation 'com.google.firebase:firebase-analytics'

    // reflection-free flavor
    implementation 'com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.5.6'
}

def getDebugGoogleMapsApiKey() {
    try {
        def configFile = new Properties()
        file("keys.properties").withInputStream { configFile.load(it) }
        return configFile.API_KEY ?: emptyString()
    } catch (ignored) {
        return emptyString()
    }
}

static def getReleaseGoogleMapsApiKey() {
    return System.getenv("GOOGLE_MAPS_API_KEY") ?: emptyString()
}

static def emptyString() {
    return "\"\""
}